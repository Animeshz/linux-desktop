{ pkgs, lib, config, ... }:

with lib;
let
  cfg = config.display;

  enabledWMs = count id (with cfg.windowManagers;
    [ awesome.enable herbstluftwm.enable hyprland.enable ]);

  enabledBars = count id (with cfg.bars;
    [ eww.enable ]);
in
{
  options = {
    display.hidpi.enable = mkEnableOption "Enable HiDPI settings or not";
    display.scale = mkOption { type = types.nullOr types.float; };
    display.cursorSize = mkOption { type = types.nullOr types.int; };
    display.autoRepeat = {
      delay = mkOption { type = types.int; default = 660; };
      rate = mkOption { type = types.int; default = 25; };
    };

    display.windowManagers = {
      awesome.enable = mkEnableOption "Setup awesome";
      herbstluftwm.enable = mkEnableOption "Setup hlwm";
      hyprland.enable = mkEnableOption "Setup hyprland";
    };

    display.bars = {
      eww.enable = mkEnableOption "Setup eww";
    };
  };

  config = mkIf (enabledWMs >= 1) {
    assertions = [
      {
        assertion = enabledWMs <= 1;
        message = "Only one of window manager can be enabled at the same time.";
      }
      {
        assertion = enabledBars <= 1;
        message = "Only one of bar can be enabled at the same time.";
      }
    ];

    home.packages = mkMerge [
      (mkIf cfg.windowManagers.awesome.enable [ pkgs.awesome ])
      (mkIf cfg.windowManagers.herbstluftwm.enable [ pkgs.herbstluftwm ])
      (mkIf cfg.windowManagers.hyprland.enable [ pkgs.hyprland ])
      (mkIf cfg.bars.eww.enable [ pkgs.eww ])
    ];

    xdg.configFile."herbstluftwm/autostart" = mkIf cfg.windowManagers.herbstluftwm.enable {
      source = ./herbstluftwm/autostart;
      executable = true;
      # onChange = "${config.xdg.configHome}/herbstluftwm/autostart";
    };

    xdg.configFile."eww" = mkIf cfg.bars.eww.enable {
      source = ./eww;
      recursive = true;
      executable = true;
      # onChange = "${pkgs.eww} kill && sleep 0.25 && ${pkgs.eww} open topbar";
    };

    home.file.".Xresources" = {
      executable = true;
      text = ''
        ! WARNING: This file is generated by home-manager.
        ! Do not edit it!

        Xft.dpi: ${toString (builtins.floor (96 * cfg.scale))}
        Xcursor.size: ${toString cfg.cursorSize}
      '';
    };

    # TODO: support commands for more than this
    home.file.".xinitrc" = let
      windowManagerCommand = if cfg.windowManagers.herbstluftwm.enable then "${pkgs.herbstluftwm}/bin/herbstluftwm --locked" else "";
      barCommand = if cfg.bars.eww.enable then "${pkgs.eww}/bin/eww open topbar" else "";
    in
    {
      executable = true;
      text = ''
        # WARNING: This file is generated by home-manager.
        # Do not edit it!

        # Syncs Xresources
        xrdb -merge ~/.Xresources &
        export GDK_DPI_SCALE=${toString cfg.scale}
        export QT_SCALE_FACTOR=${toString cfg.scale}
        export QT_AUTO_SCREEN_SCALE_FACTOR=0
        ${if cfg.hidpi.enable then "export QT_ENABLE_HIDPI_SCALING=1" else ""}

        # Topbar
        ${barCommand} &

        # Key hold-repeat frequency
        xset r rate ${toString cfg.autoRepeat.delay} ${toString cfg.autoRepeat.rate}

        # Touchpad configuration
        # xinput list \
        # | awk '/Touchpad/ {print $6}' \
        # | cut -d'=' -f2 \
        # | while read -r touchpad_id; do
        #    xinput --set-prop $touchpad_id 313 1      # Tap
        #    xinput --set-prop $touchpad_id 315 1      # Drag
        #    xinput --set-prop $touchpad_id 317 0      # Drag Lock
        #    xinput --set-prop $touchpad_id 310 1      # Horizontal Scroll
        #    xinput --set-prop $touchpad_id 291 1      # Natural Scrolling
        #    xinput --set-prop $touchpad_id 321 1      # Disable while Typing
        #    xinput --set-prop $touchpad_id 294 1 0 0  # Scroll Method (two-finger, edge, button)
        #    xinput --set-prop $touchpad_id 324 1 0    # Click Method (button-areas, click-finger) (see: 1)
        #    xinput --set-prop $touchpad_id 302 0.5    # Accel Speed
        #    xinput --set-prop $touchpad_id 305 1 0    # Accel Profile (adaptive, flat)
        #    xinput --set-prop $touchpad_id 277 0 0    # Send event modes (see: 2)
        #  done &

        exec dbus-launch --exit-with-session ${windowManagerCommand}

        # 1: click-finger allows only two-finger tap behave as right-click and three-finger tap as middle,
        #    whereas button-areas adds right-side and middle-side realistic click recognition additionally (to above).
        # 2: 0,0: enabled;; 1,0: disabled;; 0,1: disabled-when-external-mouse
      '';
    };
  };
}